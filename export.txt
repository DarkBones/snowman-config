```nix
# users/env/bas.nix

{ osConfig, ... }: {
  home.sessionPath = [
    "$HOME/.local/state/nix/profiles/home-manager/bin"
    "$HOME/.nix-profile/bin"
  ];

  home.sessionVariables = {
    EDITOR = "nvim";
    LANG = "en_US.UTF-8";
    FLAKE = "~/Developer/snowman";

    TEST_PATH = osConfig.sops.secrets.test.path;
  };
}
```


```nix
# home/roles/dev.nix

{ lib, pkgs, pkgsUnstable, config, ... }:
let cfg = config.roles.dev;
in {
  options.roles.dev.enable = lib.mkEnableOption "Dev role";

  config = lib.mkIf cfg.enable {
    home.packages = with pkgsUnstable; [
      neovim
      pkgs.git
      fzf
      cowsay
      pnpm
      docker
      gcc
    ];
  };
}
```


```nix
# flake.nix

{
  description = "A new Snowman user configuration";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-25.05";
    nixpkgs-unstable.url = "github:NixOS/nixpkgs/nixos-unstable";

    home-manager.url = "github:nix-community/home-manager/release-25.05";
    home-manager.inputs.nixpkgs.follows = "nixpkgs";

    disko.url = "github:nix-community/disko";
    nixos-hardware.url = "github:NixOS/nixos-hardware";

    sops-nix = {
      url = "github:mic92/sops-nix";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    snowman = {
      # url = "github:DarkBones/snowman";
      url = "github:DarkBones/snowman/main-v3--roles"; # TODO: switch to `main` when ready
      inputs.nixpkgs.follows = "nixpkgs";
    };

    # Add your pinned dotfiles here, e.g.:
    # bas-dotfiles = {
    #   url = "github:DarkBones/dotfiles";
    #   flake = false;
    # };
  };

  outputs =
    { self, nixpkgs, home-manager, sops-nix, snowman, disko, ... }@inputs:
    let
      lib = nixpkgs.lib;

      # This is YOUR inventory, not snowman's
      inv = import ./inventory.nix;

      # This is you map your dotfile inputs
      dotfilesSources = {
        # bas = inputs.bas-dotfiles;
      };

      # Standard Snowman setup
      makePkgs = system:
        import nixpkgs {
          inherit system;
          config.allowUnfree = true;
        };
      makePkgsUnstable = system:
        import inputs.nixpkgs-unstable {
          inherit system;
          config.allowUnfree = true;
        };

      mkNixosSpecialArgs = name: attrs: {
        inherit home-manager inv sops-nix dotfilesSources disko;
        pkgsUnstable = makePkgsUnstable attrs.system;
        modulesPath = "${nixpkgs}/nixos/modules";
        currentHost = name;

        extraHomeImports = [
          ./home/roles/dev.nix
        ];
      };

      mkHost = name: attrs:
        lib.nixosSystem {
          system = attrs.system;
          specialArgs = mkNixosSpecialArgs name attrs;
          modules = [
            # Import the modules from the Snowman input
            snowman.nixosModules.default

            home-manager.nixosModules.home-manager

            # (Optional) A place for your host-specific .nix files
            # ./hosts/${name}.nix
          ];
        };
    in {
      nixosConfigurations = lib.mapAttrs mkHost inv.hosts;

      # ... (homeConfigurations, etc., you can extend this)
    };
}
```


```nix
# inventory.nix

{
  release = "25.05";

  hosts = {
    vm-snowman = {
      system = "x86_64-linux";
      mutableUsers = false; # Defaults to `true` if omitted
      hostname = "vm-snowman"; # Optional, defaults to hosts.[name]
      provision.disk.enable = false;
      # useDHCP = true; # Default if omitted

      secrets = {
        sopsFile = ./hosts/secrets/vm-snowman_secrets.yml;

        items = {
          test = {
            # path inside the YAML
            key = "test";
            # file owner/group/mode for the concrete secret file
            owner = "root";
            group = "root";
            mode = "0400";
          };

          wireguard-private-key = {
            key = "wireguard-private-key";
            owner = "root";
            group = "root";
            mode = "0400";
          };
        };
      };

      profiles = [ "qemu-guest" ]; # ONLY for VMs. On normal machines, simply omit this key

      hardware = {
        boot = { firmware = "bios"; }; # "bios" | "efi"
        # disk = { device = "/dev/vda"; }; # VM disk
        bootDevice = "/dev/vda";
        fs = {
          type = "ext4";
          partition = 1; # /dev/vda1
          # swapGiB = 0;
        };
      };

      users = [ "bas" ];

      bootstrap.usb = {
        enable = true;
        label = "SNOWMANKEY";
        path = "/mnt/snowman";
        keyFile = "snowman.key";
        fsType = "vfat";
      };
    };
    # macs later via nix-darwin
  };

  users = {
    bas = {
      uid = 1000;
      homeManaged = true;
      groups = [ "wheel" ];
      shell = "zsh";
      sshPubKeys = [ (builtins.readFile ./users/keys/bas-arch.pub) ];

      secrets = {
        sopsFile = ./users/secrets/bas_secrets.yml;
        keys = [ "password_hash" "test" ];
        userPasswordHashKey = "password_hash";
      };
      # initialPassword = "changeme";

      envFile = ./users/env/bas.nix;

      roles = {
        dev.enable = true;
        ssh.enable = true;
        secrets.enable = true;

        dotfiles = {
          enable = true;

          ############################################################
          ## MODE SELECTION
          ##
          ## If `sourceKey` resolves in dotfilesSources (specialArgs),
          ## we use *pinned mode* (flake input in the Nix store).
          ##
          ## If `sourceKey` is null or doesn't resolve, we fall back
          ## to *git mode* (clone/pull at activation time).
          ############################################################

          # Pinned mode (reproducible; uses flake input)
          # default = home.username if omitted
          # sourceKey = "bas";

          ############################################################
          ## GIT MODE (NON-REPRODUCIBLE)
          ##
          ## Only used when pinned mode is not active.
          ############################################################
          repo = "git@github.com:DarkBones/.dotfiles.git";
          dir = "Developer/dotfiles";
          branch = "nix"; # TODO: Make `main` default later
          sparse = [ "nvim" "zsh" ];

          ############################################################
          ## SHARED SETTINGS (BOTH MODES)
          ############################################################
          linkMap = {
            ".config/nvim" = "nvim/.config/nvim";
            ".zsh" = "zsh/.zsh";
            ".zshrc" = "zsh/.zshrc";
          };

          # TODO (future): deployTokenKey / deployKeySecret for private SSH access
        };
      };
    };
  };
}
```


